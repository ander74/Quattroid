apply plugin: 'com.android.application'

// Para evitar exponer públicamente en GitHub las claves privadas de las APIs que utiliza
// esta aplicación, éstas se han colocado en un archivo externo cuyas propiedades se cargan
// en este archivo por medio de las siguientes tres líneas de código.

// Creamos una variable en la que se carga el archivo que contiene las claves privadas.
def keystorePropertiesFile = rootProject.file("keystore.properties")

// Inicializamos un nuevo objeto Properties().
def keystoreProperties = new Properties()

// Cargamos el archivo con las claves privadas en el nuevo objeto Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))



android {
    signingConfigs {
        // Accedemos a las claves de firma del APK a través del objeto keystoreProperties creado
        // anteriormente, para ocultar estas claves en los repositorios de código.
        Quattroid {
            keyAlias keystoreProperties["keyAlias"]
            storeFile file(keystoreProperties["storeFile"])
            keyPassword keystoreProperties["keyPassword"]
            storePassword keystoreProperties["storePassword"]
        }
    }
    compileSdkVersion 33
    buildToolsVersion '33.0.2'
    defaultConfig {
        applicationId "com.quattro.quattroid"
        minSdkVersion 24
        targetSdkVersion 33
        versionCode 192 // Usar versiones de tres cifras, aunque sean versiones fijas: 1.7 = 170
        versionName '1.9.2'
        signingConfig signingConfigs.Quattroid
        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.Quattroid
            // Creamos una variable con la clave secreta de Dropbox, accesible desde el código
            // java. Esta clave secreta se encuentra en el objeto keystoreProperties, fuera del
            // alcance de los repositorios de código.
            buildConfigField "String", "DropboxAppKey", keystoreProperties["dropboxAppKey"]
        }
        debug {
            signingConfig signingConfigs.Quattroid
            // Creamos una variable con la clave secreta de Dropbox, accesible desde el código
            // java. Esta clave secreta se encuentra en el objeto keystoreProperties, fuera del
            // alcance de los repositorios de código.
            buildConfigField "String", "DropboxAppKey", keystoreProperties["dropboxAppKey"]
        }
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/DEPENDENCIES', 'META-INF/LICENSE', 'META-INF/LICENSE.txt', 'META-INF/license.txt', 'META-INF/NOTICE', 'META-INF/NOTICE.txt', 'META-INF/notice.txt', 'META-INF/ASL2.0', 'META-INF/project.properties']
        }
    }

    productFlavors {
    }
    compileOptions {
        sourceCompatibility = "11"
        targetCompatibility = "11"
    }
    namespace 'com.quattro.quattroid'
}

dependencies {
    implementation "androidx.multidex:multidex:2.0.1"
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.dropbox.core:dropbox-core-sdk:5.1.1'
    //implementation 'com.squareup.okhttp3:okhttp:3.12.2'
    implementation 'com.itextpdf:kernel:7.0.2'
    implementation 'com.itextpdf:io:7.0.2'
    implementation 'com.itextpdf:layout:7.0.2'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'joda-time:joda-time:2.10.3'
}
