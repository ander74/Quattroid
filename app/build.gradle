apply plugin: 'com.android.application'

// Para evitar exponer públicamente en GitHub las claves privadas de las APIs que utiliza
// esta aplicación, éstas se han colocado en un archivo externo cuyas propiedades se cargan
// en este archivo por medio de las siguientes tres líneas de código.

// Creamos una variable en la que se carga el archivo que contiene las claves privadas.
def keystorePropertiesFile = rootProject.file("keystore.properties")

// Inicializamos un nuevo objeto Properties().
def keystoreProperties = new Properties()

// Cargamos el archivo con las claves privadas en el nuevo objeto Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))



android {
    signingConfigs {
        // Accedemos a las claves de firma del APK a través del objeto keystoreProperties creado
        // anteriormente, para ocultar estas claves en los repositorios de código.
        Quattroid {
            keyAlias keystoreProperties['keyAlias']
            storeFile file(keystoreProperties['storeFile'])
            keyPassword keystoreProperties['keyPassword']
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    defaultConfig {
        applicationId "com.quattro.quattroid"
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 16
        versionName '1.6'
        signingConfig signingConfigs.Quattroid
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.Quattroid
            // Creamos una variable con la clave secreta de Dropbox, accesible desde el código
            // java. Esta clave secreta se encuentra en el objeto keystoreProperties, fuera del
            // alcance de los repositorios de código.
            buildConfigField 'String', "DropboxAppKey", keystoreProperties['dropboxAppKey']
        }
        debug {
            signingConfig signingConfigs.Quattroid
            // Creamos una variable con la clave secreta de Dropbox, accesible desde el código
            // java. Esta clave secreta se encuentra en el objeto keystoreProperties, fuera del
            // alcance de los repositorios de código.
            buildConfigField 'String', "DropboxAppKey", keystoreProperties['dropboxAppKey']
        }
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/project.properties'
    }

    productFlavors {
    }
    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.dropbox.core:dropbox-core-sdk:2.0.3'
    implementation 'com.itextpdf:kernel:7.0.2'
    implementation 'com.itextpdf:io:7.0.2'
    implementation 'com.itextpdf:layout:7.0.2'
}
